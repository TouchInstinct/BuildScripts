def getAndroidProjectSources

apply from: "$buildScriptsDir/gradle/commonStaticAnalysis.gradle"

gradle.projectsEvaluated {

    tasks.withType(JavaCompile) {
        options.compilerArgs <<
                "-Xlint:cast" <<
                "-Xlint:divzero" <<
                "-Xlint:empty" <<
                "-Xlint:deprecation" <<
                "-Xlint:finally" <<
                "-Xlint:overrides" <<
                "-Xlint:path" <<
                "-Werror"
    }

    def excludes = rootProject.extensions.findByName("staticAnalysisExcludes")
    def pmdEnabled = rootProject.extensions.findByName("pmdEnabled") ?: false

    def androidSources = getAndroidProjectSources(excludes)
    def androidStaticAnalysisTasks = getStaticAnalysisTaskNames(true, androidSources, null, pmdEnabled)
    def androidIdeaFormatTask = getIdeaFormatTask(androidSources)

    task staticAnalysisWithFormatting {
        if (androidIdeaFormatTask != null) {
            androidStaticAnalysisTasks.each { task ->
                tasks.findByName(task).mustRunAfter(androidIdeaFormatTask)
            }
            dependsOn androidIdeaFormatTask
        }
        dependsOn androidStaticAnalysisTasks
        doFirst {
            generateReport(true)
        }
    }

    task staticAnalysis {
        dependsOn androidStaticAnalysisTasks
        doFirst {
            generateReport(true)
        }
    }

    android.applicationVariants.all { variant ->
        task("staticAnalysis${variant.name.capitalize()}") {
            dependsOn getStaticAnalysisTaskNames(true, androidSources, variant, pmdEnabled)
            doFirst { generateReport(true) }
        }
    }

}

getAndroidProjectSources = { excludes ->
    def sources = new ArrayList<String>()
    for (def project : rootProject.subprojects) {
        if (!project.subprojects.isEmpty() || (excludes != null && excludes.contains(project.path))) {
            continue
        }

        def sourcesDirectory = new File(project.projectDir.path, 'src')
        if (!sourcesDirectory.exists() || !sourcesDirectory.isDirectory()) {
            continue
        }

        for (def sourceFlavorDirectory : sourcesDirectory.listFiles()) {
            def javaSourceDirectory = new File(sourceFlavorDirectory.path, 'java')
            def kotlinSourceDirectory = new File(sourceFlavorDirectory.path, 'kotlin')

            if (javaSourceDirectory.exists() && javaSourceDirectory.isDirectory()) {
                sources.add(javaSourceDirectory.absolutePath)
            }
            if (kotlinSourceDirectory.exists() && kotlinSourceDirectory.isDirectory()) {
                sources.add(kotlinSourceDirectory.absolutePath)
            }
        }
    }
    return sources
}
